# vim: set ft=zsh :
# shellcheck disable=all
#
# Login Shell Configuration
# Sourced by: LOGIN shells only (SSH, terminal launch, explicit zsh -l)
# Purpose: Expensive PATH setup and platform-specific initialization
# Why not zshenv: Keeps env lean, runs once per login instead of every subshell

# Guard against re-sourcing (safe to manually source if needed)
[[ -n "$_ZPROFILE_SOURCED" ]] && return
typeset -g _ZPROFILE_SOURCED=1

# PATH Configuration
if [[ $OS_NAME == "Darwin" ]]; then
    # Initiate Homebrew
    if [[ $OS_ARCH == "arm64" ]]; then
        eval "$(/opt/homebrew/bin/brew shellenv)"
    else
        eval "$(/usr/local/bin/brew shellenv)"
    fi

    # Additional Paths
    typeset -U path
    path=(
        "$XDG_BIN_HOME"
        "${XDG_DATA_HOME}/google-cloud-sdk/bin"
        "${HOMEBREW_PREFIX}/opt/coreutils/libexec/gnubin"
        "${HOMEBREW_PREFIX}/opt/curl/bin"
        "${HOMEBREW_PREFIX}/opt/findutils/libexec/gnubin"
        "${HOMEBREW_PREFIX}/opt/gnu-indent/libexec/gnubin"
        "${HOMEBREW_PREFIX}/opt/gnu-sed/libexec/gnubin"
        "${HOMEBREW_PREFIX}/opt/gnu-tar/libexec/gnubin"
        "${HOMEBREW_PREFIX}/opt/gnu-which/libexec/gnubin"
        "${HOMEBREW_PREFIX}/opt/grep/libexec/gnubin"
        "${HOMEBREW_PREFIX}/opt/make/libexec/gnubin"
        "${HOMEBREW_PREFIX}/opt/fzf/bin"
        $path
    )

    # FZF
    export FZF_SHELL_DIR="${HOMEBREW_PREFIX}/opt/fzf/shell"

elif [[ $OS_NAME = "Linux" ]]; then
    # Additional Paths (Debian/Ubuntu manual installs)
    if [[ ! "$OS_DISTRO" == "nixos" ]]; then
        typeset -U path
        path=(
            "$XDG_BIN_HOME"
            "${XDG_DATA_HOME}/google-cloud-sdk/bin"
            "${HOME}/.fzf/bin"
            "/usr/libexec/docker/cli-plugins"
            $path
        )
    fi

    # FZF shell integration path (distro-specific)
    if [[ "$OS_DISTRO" == "nixos" ]]; then
        if [[ -n "${commands[fzf-share]}" ]]; then
            source "$(fzf-share)/key-bindings.zsh"
            source "$(fzf-share)/completion.zsh"
        fi
    else
        # Debian/Ubuntu: manual install to ~/.fzf
        export FZF_SHELL_DIR="${HOME}/.fzf/shell"
    fi
fi

# LS_COLORS (used by ls and completion colors)
if [[ -f "${HOME}/.dir_colors" ]]; then
    eval "$(dircolors -b "${HOME}/.dir_colors")"
fi
